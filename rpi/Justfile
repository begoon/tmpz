set dotenv-load := true
set export := true

default: run-http

preset:
    @echo WIFI_SSID = '"{{ env("WIFI_SSID") }}"' > settings.py
    @echo WIFI_PASSWORD = '"{{ env("WIFI_PASSWORD") }}"' >> settings.py
    @echo WS_HOST = '"{{ env("WS_HOST") }}"' >> settings.py
    @echo WS_PORT = {{ env("WS_PORT") }} >> settings.py
    @echo WS_PATH = '"{{ env("WS_PATH") }}"' >> settings.py

    @echo I2C_CHANNEL = {{ env("I2C_CHANNEL") }} >> settings.py
    @echo I2C_SDA = {{ env("I2C_SDA") }} >> settings.py
    @echo I2C_SCL = {{ env("I2C_SCL") }} >> settings.py

install-settings: preset
    uvx mpremote fs cp --force settings.py :

# http.py
run-http: install-settings main-http run

main-http:
    cp http.py main.py

# ws.py
run-ws: reset install-settings main-ws run

main-ws:
    cp ws.py main.py

# oled.py
run-oled: reset install-settings main-oled install-ssd1306 run

main-oled:
    cp oled.py main.py

# 
install-ssd1306: preset
    uvx mpremote fs cp ssd1306.py :

# ---
run:
    uvx mpremote run main.py

packages:
    uv pip install -U micropython-rp2-pico_w-stubs --target typings

reset:
    uvx mpremote soft-reset
